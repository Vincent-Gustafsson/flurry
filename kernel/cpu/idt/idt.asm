%macro PUSH_REGS 0
    push rax
    push rbx
    push rcx
    push rdx
    push rsi
    push rdi
    push rbp
    push r8
    push r9
    push r10
    push r11
    push r12
    push r13
    push r14
    push r15
%endmacro

%macro POP_REGS 0
    pop r15
    pop r14
    pop r13
    pop r12
    pop r11
    pop r10
    pop r9
    pop r8
    pop rbp
    pop rdi
    pop rsi
    pop rdx
    pop rcx
    pop rbx
    pop rax
%endmacro

%macro INTERRUPT_NAME 1
    dq __interrupt%1
%endmacro

%macro isr_err_stub 1
isr_stub_%1:
    push qword %1
    jmp interrupt_stub
%endmacro

%macro isr_no_err_stub 1
isr_stub_%1:
    push qword 0   ; no error
    push qword %1
    jmp interrupt_stub
%endmacro

extern interrupt_dispatch

interrupt_stub:
    PUSH_REGS

    mov rdi, rsp
    call interrupt_dispatch
    mov rsp, rax

    POP_REGS
    ;remove the vector number + error code
    add rsp, 16

    iretq

isr_no_err_stub 0
isr_no_err_stub 1
isr_no_err_stub 2
isr_no_err_stub 3
isr_no_err_stub 4
isr_no_err_stub 5
isr_no_err_stub 6
isr_no_err_stub 7
isr_err_stub    8
isr_no_err_stub 9
isr_err_stub    10
isr_err_stub    11
isr_err_stub    12
isr_err_stub    13
isr_err_stub    14
isr_no_err_stub 15
isr_no_err_stub 16
isr_err_stub    17
isr_no_err_stub 18
isr_no_err_stub 19
isr_no_err_stub 20
isr_no_err_stub 21
isr_no_err_stub 22
isr_no_err_stub 23
isr_no_err_stub 24
isr_no_err_stub 25
isr_no_err_stub 26
isr_no_err_stub 27
isr_no_err_stub 28
isr_no_err_stub 29
isr_err_stub    30
isr_no_err_stub 31

global isr_stubs
isr_stubs:
    %assign i 0
    %rep    32
        dq isr_stub_%+i ; use DQ instead if targeting 64-bit
    %assign i i+1
%endrep